---
title: MMKV剖析
date: 2019-11-26 15:16:17
categories: Tech
tags:
    - iOS
    - linux
---

[MMKV](https://github.com/Tencent/MMKV)是由腾讯开源的一款高性能通用`key-value`组件。写这篇文章的目的一是学习怎样从零开始实现一个`key-value`组件，二是探寻`MMKV`的实现细节并从中学习。由于iOS和Android的实现略有不同，这里只分析iOS版本的实现。

### 准备知识
#### mmap
[mmap](https://en.wikipedia.org/wiki/Mmap)实际上是一种内存映射技术，它能够将一个文件或者其他对象映射到地址空间中，这样一来磁盘地址和进程的虚拟内存地址就有了一一对应关系。进程可以直接操作（读写）这一段内存，而系统会帮助我们自动将脏数据写回磁盘文件。相比于进程直接打开文件读写，`mmap`的一个优势是系统会自动回写脏数据，所以不必担心因为进程Crash而导致数据丢失的问题。另一个优势是它的效率更高。因为为了提高文件操作的效率和保护磁盘，常规的文件操作有着页缓存机制，读过程中文件页先会被拷贝到页缓存中，而页缓存处在内核空间，用户进程无法直接寻址，所以它又会被拷贝到内存的用户空间中。写过程同理。所以每一次读/写都需要两次拷贝过程。
<!-- more -->

`mmap`的使用不算复杂，主要函数有以下几个：
```c
// 将文件fd映射到内存中
void *mmap(void *start, size_t length, int prot, int flags, int fd, off_t offset);
// 解除映射
int munmap(void * addr, size_t len)
// 同步
int msync(void *addr, size_t len, int flags)
// 锁定内存空间地址
int mlock(const void *, size_t);
// 解锁内存空间地址
int munlock(const void *, size_t);
```
需要注意的是，内存只能按页进行映射，所以当指定的`length`不是按页大小对齐的时候，内核会填充一部分长度对齐到页大小。

#### Protocol Buffer
[Protocol Buffer](https://github.com/protocolbuffers/protobuf)是由Google开源的一种轻便搞笑的结构化数据存储格式，它可以用来序列化和反序列化结构化的数据，适合做数据存储以及RPC数据交换格式。它的优势是序列化后的数据更小、序列化和反序列过程更快、使用起来更简单。缺点是序列化后的数据不易阅读。编码的原理就不在这里赘述了，网上文章一大把。


### 剖
让我们来思考一下，一个最基本的`key-value`组件无外乎这么几个组成部分：
- 数据持久化（序列化和反序列化）
- 内存缓存
- 数据读写
- 加密存储（可选）

在来看一眼整个`MMKV`项目的目录结构
<img src="files.png" alt="">

可以发现文件不算多，其中`aes`目录下的肯定是和加/解密有关的代码，可以先放一边。`MMKVLog.h/mm`是和Log相关，也可以先放一旁。`LRUCache.hpp`和`MemoryFile.h/mm`当前的版本暂时没有用到，是为将来的大文件feature准备的。有兴趣的可以看`dev_bigfile`分支。

- `MiniCodedInputData.h/mm`负责读取数据
- `MiniCodedOutputData.h/mm`负责写入数据
- `MiniPBCoder.h/mm`负责编/解码数据
- `MiniPBEncodeItem.h`编码数据的数据结构
- `MiniPBUtility.h/mm`编/解码相关的工具类
- `MMKV.h/mm`MMKV核心类
- `MMKVMetaInfo.hpp`meta数据的数据结构

#### 数据持久化
`MMKV`底层使用了`protobuf`来对数据进行序列化和反序列，但是因为`key-value`组件存储的数据结构相对简单（都是`String`-`Data`类型），只用实现`protobuf`的部分功能，所以相关类都加上了`Mini`的前缀。
`MMKV`存储的文件结构也比较简单，文件的头部使用了32bit存储文件的实际大小。这是因为文件的末尾可能留白（为了应对新增数据），数据大小可能小于文件本身的大小。在32bit之后就按照`protobuf`的编码方式（`Varint`）依次写（`String`,`Data`），（`String`,`Data`）...
```
ActualSize | String | Data | String | Data | ... | String | Data
-- 32bit --
```
使用`protobuf`的一个问题是标准的`protobuf`不支持增量更新，每次写入必须全量写入。`MMKV`的做法是直接将新增的`kv`对象序列化后添加到文件的末尾，这样一来有两个问题：
1. 在文件中会存在一个`key`对应多个`value`的情况出现。
2. 反复`set`一个`key`会导致文件大小会持续增长。

针对问题`1`，在从文件中加载数据时，针对相同`key`，只需要反复使用后来的`value`覆盖前面的`value`就能保证数据正确。
针对问题`2`，在适当的时机对文件进行重排，删除重复数据，进行全量写入。

#### 内存缓存
`key-value`的内存缓存结构没啥好说的，就是一个`NSMutableDictionary`类型的字典，`key`是`NSString`，`value`是`NSData`。

#### 数据读写
读操作时，先判断内存缓存是否需要更新，如需要，重新从文件中加载数据。否则直接从内存缓存中查找对应的数据并反序列化后返回。
写操作时，分别向内存缓存和`mmap`写入。如果发现文件大小不足，则对文件进行重排（去重），重排后如果大小还是不够，则尝试增加文件大小。

#### 其他
`MMKV`还提供了文件的`CRC`校验和数据加密功能，如果`CRC`校验失败还能尝试恢复数据。这些附属功能不影响正常的工作流程，有机会在分析学习。

### 析
从剖来看，我们挑选几个重要的部分来分析代码，

#### 文件加载
在`MMKV`的`initWithMMapID:cryptKey:path:`方法中，我们可以发现其调用了`loadFromFile`，它负责了将文件映射到内存的工作。

`meta file`和`m_cryptor`部分可以先跳过，一个是和`CRC`校验相关，一个是和加/解密相关。

```objc
- (void)loadFromFile {
	[self prepareMetaFile];
	if (m_metaFilePtr != nullptr && m_metaFilePtr != MAP_FAILED) {
		m_metaInfo.read(m_metaFilePtr);
	}
	if (m_cryptor) {
		if (m_metaInfo.m_version >= 2) {
			m_cryptor->reset(m_metaInfo.m_vector, sizeof(m_metaInfo.m_vector));
		}
	}

	// 1
	m_fd = open(m_path.UTF8String, O_RDWR, S_IRWXU);
	if (m_fd < 0) {
		MMKVError(@"fail to open:%@, %s", m_path, strerror(errno));
	} else {
		m_size = 0;
		struct stat st = {};
		if (fstat(m_fd, &st) != -1) {
			// 2
			m_size = (size_t) st.st_size;
		}
		// 3
		// round up to (n * pagesize)
		if (m_size < DEFAULT_MMAP_SIZE || (m_size % DEFAULT_MMAP_SIZE != 0)) {
			m_size = ((m_size / DEFAULT_MMAP_SIZE) + 1) * DEFAULT_MMAP_SIZE;
			if (ftruncate(m_fd, m_size) != 0) {
				MMKVError(@"fail to truncate [%@] to size %zu, %s", m_mmapID, m_size, strerror(errno));
				m_size = (size_t) st.st_size;
				return;
			}
		}
		// 4
		m_ptr = (char *) mmap(nullptr, m_size, PROT_READ | PROT_WRITE, MAP_SHARED, m_fd, 0);
		if (m_ptr == MAP_FAILED) {
			MMKVError(@"fail to mmap [%@], %s", m_mmapID, strerror(errno));
		} else {
			// 5
			const int offset = pbFixed32Size(0);
			NSData *lenBuffer = [NSData dataWithBytesNoCopy:m_ptr length:offset freeWhenDone:NO];
			@try {
				m_actualSize = MiniCodedInputData(lenBuffer).readFixed32();
			} @catch (NSException *exception) {
				MMKVError(@"%@", exception);
			}
			MMKVInfo(@"loading [%@] with %zu size in total, file size is %zu", m_mmapID, m_actualSize, m_size);
			if (m_actualSize > 0) {
				// 6
				bool loadFromFile, needFullWriteback = false;
				if (m_actualSize < m_size && m_actualSize + offset <= m_size) {
					if ([self checkFileCRCValid] == YES) {
						loadFromFile = true;
					} else {
						loadFromFile = false;
						if (g_callbackHandler && [g_callbackHandler respondsToSelector:@selector(onMMKVCRCCheckFail:)]) {
							auto strategic = [g_callbackHandler onMMKVCRCCheckFail:m_mmapID];
							if (strategic == MMKVOnErrorRecover) {
								loadFromFile = true;
								needFullWriteback = true;
							}
						}
					}
				} else {
					MMKVError(@"load [%@] error: %zu size in total, file size is %zu", m_mmapID, m_actualSize, m_size);
					loadFromFile = false;
					if (g_callbackHandler && [g_callbackHandler respondsToSelector:@selector(onMMKVFileLengthError:)]) {
						auto strategic = [g_callbackHandler onMMKVFileLengthError:m_mmapID];
						if (strategic == MMKVOnErrorRecover) {
							loadFromFile = true;
							needFullWriteback = true;
							[self writeActualSize:m_size - offset];
						}
					}
				}
				if (loadFromFile) {
					// 7
					MMKVInfo(@"loading [%@] with crc %u sequence %u", m_mmapID, m_metaInfo.m_crcDigest, m_metaInfo.m_sequence);
					NSData *inputBuffer = [NSData dataWithBytesNoCopy:m_ptr + offset length:m_actualSize freeWhenDone:NO];
					if (m_cryptor) {
						inputBuffer = decryptBuffer(*m_cryptor, inputBuffer);
					}
					m_dic = [MiniPBCoder decodeContainerOfClass:NSMutableDictionary.class withValueClass:NSData.class fromData:inputBuffer];
					m_output = new MiniCodedOutputData(m_ptr + offset + m_actualSize, m_size - offset - m_actualSize);
					if (needFullWriteback) {
						[self fullWriteBack];
					}
				} else {
					[self writeActualSize:0];
					m_output = new MiniCodedOutputData(m_ptr + offset, m_size - offset);
					[self recaculateCRCDigest];
				}
			} else {
				m_output = new MiniCodedOutputData(m_ptr + offset, m_size - offset);
				[self recaculateCRCDigest];
			}
			MMKVInfo(@"loaded [%@] with %zu values", m_mmapID, (unsigned long) m_dic.count);
		}
	}
	if (m_dic == nil) {
		m_dic = [NSMutableDictionary dictionary];
	}

	if (![self isFileValid]) {
		MMKVWarning(@"[%@] file not valid", m_mmapID);
	}

	tryResetFileProtection(m_path);
	tryResetFileProtection(m_crcPath);
	m_needLoadFromFile = NO;
}
```

1. 打开磁盘文件
2. 获取文件的大小
3. 调整文件大小对齐内存页大小
4. 使用`mmap`将文件映射到内存，`m_ptr`为文件在内存中初始地址指针
5. 读取文件中数据部分长度
6. 验证文件完整性，包括文件大小和`CRC`校验
7. 读取文件内容到`m_dic`（内存缓存），`m_output`为文件的空白部分。

总结一下，`LoadFromFile`执行完后，数据文件通过`mmap`建立起和内存空间的地址映射关系，完成了对`m_dic`（数据文件的内存缓存），`m_ptr`（文件在内存空间中的首地址）,`m_output`（文件空白区域在内存中的表示）赋值。

其中`m_output`的类型为`MiniCodedOutputData`，它负责往内存中写数据，可以理解为表示一块内存，包含三个变量：
```cpp
uint8_t *m_ptr;
size_t m_size;
int32_t m_position;
```
其中`m_ptr`表示基地址，`m_size`表示这块内存空间的大小，`m_position`表示与基地址`m_ptr`的偏移。如果`m_position == m_size`则表示这块内存已经写满了。

#### set
`set`方法最后都会调用到`setRawData:forKey:`这个方法，也就是说所有类型的`value`都会被转成`NSData`类型的数据。

```objc
- (BOOL)setRawData:(NSData *)data forKey:(NSString *)key {
	if (data.length <= 0 || key.length <= 0) {
		return NO;
	}
	// 1
	CScopedLock lock(m_lock);

	// 2
	auto ret = [self appendData:data forKey:key];
	if (ret) {
		// 3
		[m_dic setObject:data forKey:key];
		m_hasFullWriteBack = NO;
	}
	return ret;
}
```

1. 加区域锁，区域锁会随着其退出作用域而释放。
2. 调用了`appendData:forKey:`方法，下述。
3. 成功后向内存缓存也写入一份。`m_hasFullWriteBack`表示是否已经将`m_dic`的内存全量写回文件。


`appendData:forKey:`负责将新增的数据写回文件，上面谈到`protobuf`是不支持增量更新的，所以使用了在末尾添加数据来折中解决这一问题，这个方法做的就是这件事情。

```objc
- (BOOL)appendData:(NSData *)data forKey:(NSString *)key {
	// 1
	size_t keyLength = [key lengthOfBytesUsingEncoding:NSUTF8StringEncoding];
	auto size = keyLength + pbRawVarint32Size((int32_t) keyLength); // size needed to encode the key
	size += data.length + pbRawVarint32Size((int32_t) data.length); // size needed to encode the value

	// 2
	BOOL hasEnoughSize = [self ensureMemorySize:size];
	if (hasEnoughSize == NO || [self isFileValid] == NO) {
		return NO;
	}

	// 3
	BOOL ret = [self writeActualSize:m_actualSize + size];
	if (ret) {
		// 4
		ret = [self protectFromBackgroundWriting:size
		                              writeBlock:^(MiniCodedOutputData *output) {
			                              output->writeString(key);
			                              output->writeData(data); // note: write size of data
		                              }];
		if (ret) {
			static const int offset = pbFixed32Size(0);
			auto ptr = (uint8_t *) m_ptr + offset + m_actualSize - size;
			if (m_cryptor) {
				m_cryptor->encrypt(ptr, ptr, size);
			}
			[self updateCRCDigest:ptr withSize:size increaseSequence:KeepSequence];
		}
	}
	return ret;
}
```
1. 计算新增的数据需要的空间大小。
2. 确保有足够的空间来写入新数据。
3. 更新文件的实际数据大小。
4. 添加新数据到文件。

这里又需要注意`ensureMemorySize:`和`protectFromBackgroundWriting:writeBlock:`这两个方法。

`ensureMemorySize:`在文件大小不足的情况下会对文件进行重排操作，也就是删除废数据。如果空间还是不够，会`resize`文件的大小。

```objc
- (BOOL)ensureMemorySize:(size_t)newSize {
	[self checkLoadData];

	if (![self isFileValid]) {
		MMKVWarning(@"[%@] file not valid", m_mmapID);
		return NO;
	}

	// make some room for placeholder
	constexpr uint32_t /*ItemSizeHolder = 0x00ffffff,*/ ItemSizeHolderSize = 4;
	if (m_dic.count == 0) {
		newSize += ItemSizeHolderSize;
	}
	if (newSize >= m_output->spaceLeft() || m_dic.count == 0) {
		// 1
		// try a full rewrite to make space
		static const int offset = pbFixed32Size(0);
		NSData *data = [MiniPBCoder encodeDataWithObject:m_dic];
		size_t lenNeeded = data.length + offset + newSize;
		size_t avgItemSize = lenNeeded / std::max<size_t>(1, m_dic.count);
		size_t futureUsage = avgItemSize * std::max<size_t>(8, m_dic.count / 2);
		// 1. no space for a full rewrite, double it
		// 2. or space is not large enough for future usage, double it to avoid frequently full rewrite
		if (lenNeeded >= m_size || (lenNeeded + futureUsage) >= m_size) {
			size_t oldSize = m_size;
			// 2
			do {
				m_size *= 2;
			} while (lenNeeded + futureUsage >= m_size);
			MMKVInfo(@"extending [%@] file size from %zu to %zu, incoming size:%zu, future usage:%zu",
			         m_mmapID, oldSize, m_size, newSize, futureUsage);

			// 3
			// if we can't extend size, rollback to old state
			if (ftruncate(m_fd, m_size) != 0) {
				MMKVError(@"fail to truncate [%@] to size %zu, %s", m_mmapID, m_size, strerror(errno));
				m_size = oldSize;
				return NO;
			}

			if (munmap(m_ptr, oldSize) != 0) {
				MMKVError(@"fail to munmap [%@], %s", m_mmapID, strerror(errno));
			}

			// 4
			m_ptr = (char *) mmap(m_ptr, m_size, PROT_READ | PROT_WRITE, MAP_SHARED, m_fd, 0);
			if (m_ptr == MAP_FAILED) {
				MMKVError(@"fail to mmap [%@], %s", m_mmapID, strerror(errno));
			}

			// check if we fail to make more space
			if (![self isFileValid]) {
				MMKVWarning(@"[%@] file not valid", m_mmapID);
				return NO;
			}
			// keep m_output consistent with m_ptr -- writeAcutalSize: may fail
			delete m_output;
			m_output = new MiniCodedOutputData(m_ptr + offset, m_size - offset);
			m_output->seek(m_actualSize);
		}

		if (m_cryptor) {
			[self updateIVAndIncreaseSequence:KeepSequence];
			m_cryptor->reset(m_metaInfo.m_vector, sizeof(m_metaInfo.m_vector));
			auto ptr = (unsigned char *) data.bytes;
			m_cryptor->encrypt(ptr, ptr, data.length);
		}

		if ([self writeActualSize:data.length] == NO) {
			return NO;
		}

		delete m_output;
		m_output = new MiniCodedOutputData(m_ptr + offset, m_size - offset);
		BOOL ret = [self protectFromBackgroundWriting:m_actualSize
		                                   writeBlock:^(MiniCodedOutputData *output) {
			                                   output->writeRawData(data);
		                                   }];
		if (ret) {
			[self recaculateCRCDigest];
		}
		return ret;
	}
	return YES;
}

```

1. 计算`lenNeeded`（重排后的数据大小 + 新增的数据大小），`futureUsage`（未来可能需要的大小）。
2. 寻找文件合适的新大小，每次`*2`。
3. 更新文件大小，并解除之前的文件内存映射。
4. 重新建立文件内存映射关系，更新`m_ptr`和`m_output`。

`protectFromBackgroundWriting:writeBlock:`主要是为iOS系统设计的。当app处在后台时，它的内存空间可能不受控制，强行操作可能会造成Crash。

```objc
- (BOOL)protectFromBackgroundWriting:(size_t)size writeBlock:(void (^)(MiniCodedOutputData *output))block {
	if (g_isInBackground) {
		static const int offset = pbFixed32Size(0);
		static const int pagesize = getpagesize();
		size_t realOffset = offset + m_actualSize - size;
		size_t pageOffset = (realOffset / pagesize) * pagesize;
		size_t pointerOffset = realOffset - pageOffset;
		size_t mmapSize = offset + m_actualSize - pageOffset;
		char *ptr = m_ptr + pageOffset;
		// 1
		if (mlock(ptr, mmapSize) != 0) {
			MMKVError(@"fail to mlock [%@], %s", m_mmapID, strerror(errno));
			// just fail on this condition, otherwise app will crash anyway
			//block(m_output);
			return NO;
		} else {
			@try {
				// 2
				// ptr + pointerOffset = m_ptr + pageOffset + pointerOffset 
				// = m_ptr + pageOffset + realOffset - pageOffset
				// = m_ptr + realOffset
				// = m_ptr + offset + m_actualSize - size
				// m_actualSize已经包含了size
				MiniCodedOutputData output(ptr + pointerOffset, size);
				block(&output);
				m_output->seek(size);
			} @catch (NSException *exception) {
				MMKVError(@"%@", exception);
				return NO;
			} @finally {
				munlock(ptr, mmapSize);
			}
		}
	} else {
		block(m_output);
	}

	return YES;
}
```

1. 尝试使用`mlock`锁住`mmap`的内存区域。
2. 新建一个`output`给回调使用，完成后将`m_output`的指针向后移`size`。

需要注意的是这里的`m_actualSize`在调用这个方法前已经包含了`size`，所以`output`的基地址是`ptr + pointerOffset`。

#### get
`get`一系列方法最终可能会调到`getRawDataForKey:`
```objc
- (NSData *)getRawDataForKey:(NSString *)key {
	CScopedLock lock(m_lock);
	[self checkLoadData];
	return [m_dic objectForKey:key];
}
```
没啥特别的东西，过程无非是加锁->检查是否需要从文件加载->从内存缓存中获取。读出来的数据是`NSData`的类型，所以需要再加一层反序列化的逻辑将其还原。

#### remove
对于单个`key`移除的操作，`MMKV`分别更新`m_dic`和文件内容。

```objc
- (void)removeValueForKey:(NSString *)key {
	if (key.length <= 0) {
		return;
	}
	CScopedLock lock(m_lock);
	[self checkLoadData];

	if ([m_dic objectForKey:key] == nil) {
		return;
	}
	[m_dic removeObjectForKey:key];
	m_hasFullWriteBack = NO;

	static NSData *data = [NSData data];
	[self appendData:data forKey:key];
}
```
由于不能增量更新，所以对于`remove`一个值来说，`MMKV`使用了一个空的`NSData`来表示，同时在`m_dic`中移除这个`key`。

对于批量移除`key`s的操作，`MMKV`采取了全量写回的策略。
```objc
- (void)removeValuesForKeys:(NSArray *)arrKeys {
	if (arrKeys.count <= 0) {
		return;
	}
	CScopedLock lock(m_lock);
	[self checkLoadData];

	[m_dic removeObjectsForKeys:arrKeys];
	m_hasFullWriteBack = NO;

	MMKVInfo(@"remove [%@] %lu keys, %lu remain", m_mmapID, (unsigned long) arrKeys.count, (unsigned long) m_dic.count);

	[self fullWriteBack];
}
```


### Reference
[MMKV 原理](https://github.com/Tencent/MMKV/wiki/design)
[认真分析mmap：是什么 为什么 怎么用](https://www.cnblogs.com/huxiao-tee/p/4660352.html)
[Google Protocol Buffer 的使用和原理](https://www.ibm.com/developerworks/cn/linux/l-cn-gpb/index.html)
[Protobuf编码原理](https://www.jianshu.com/p/fab5810384e3)
[高效的数据压缩编码方式 Protobuf](https://halfrost.com/protobuf_encode/)